plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.android.kotlin)
    alias(libs.plugins.kotlin.compose)
}

android {
    def apk = "StatusBarWatermark"

    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)

    def keyKeyStoreFile = file(properties.getProperty('keyStoreFile'))
    def keyKeyStorePassword = properties.getProperty('keyStorePassword')
    def keyKeyAlias = properties.getProperty('keyAlias')
    def keyKeyAliasPassword = properties.getProperty('keyAliasPassword')

    signingConfigs {
        release {
            storeFile keyKeyStoreFile
            storePassword keyKeyStorePassword
            keyAlias keyKeyAlias
            keyPassword keyKeyAliasPassword
            enableV1Signing true
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
    }

    namespace 'com.hchen.statusbarwatermark'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "com.hchen.statusbarwatermark"
        minSdk 35
        targetSdk 36
        // noinspection HighAppVersionCode
        versionCode 2026013100
        versionName "1.0"
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "${apk}-v${variant.versionName}(${variant.versionCode})_${variant.buildType.name}.apk"
        }
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "/*.txt"
            excludes += "/*.bin"
            excludes += "/*.json"
            excludes += 'DebugProbesKt.bin'
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    kotlinOptions {
        jvmTarget = '21'
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.annotation

    implementation platform(libs.compose.bom)
    implementation libs.foundation.android
    implementation libs.activity.compose
    implementation libs.miuix.android
    implementation libs.miuix.icons.android

    compileOnly libs.api
    implementation libs.hooktool
}